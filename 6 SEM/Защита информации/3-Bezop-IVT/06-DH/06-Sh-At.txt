> # Демонстрация криптографического протокола Шамира:
> #
> # (1) генерация ключей:
> #    p - открытый ключ,
> #    (cA,dA) - закрытыe ключи абонента A;
> #    (cB,dB) - закрытыe ключи абонента B;
> #
> # (2) передачи и приёма текста по методу Шамира;
> # (3) атака на шифр Шамира с помощью решения (?!)
> #     задачи дискретного логарифмирования
> # 
> # **************
> with(numtheory):
> # ---------------------------------
> # Выбор  о т к р ы т о г о  ключа p
> # ("почти" автоматический) 
> # ---------------------------------------------------------------
> p:=nextprime(234711212312123211912334); bits:=1+floor(log[2](p)); 
> #
> # -----------------------------------------------------
> # Выбор первого  з а к р ы т о г о  ключа cA абонента A
> # ("почти" автоматический)
> # ------------------------
> cA:=nextprime(10123); 
> # -----------------------------------------------------
> # Выбор второго  з а к р ы т о г о  ключа dA абонента A
> # -----------------------------------------------------
> dA:=cA &^ (phi(p-1)-1) mod (p-1);
> # -----------------------------------------------------
> # Выбор первого  з а к р ы т о г о  ключа cB абонента B
> # ("почти" автоматический)
> # ------------------------
> cB:=nextprime(123451); 
> # -----------------------------------------------------
> # Выбор второго  з а к р ы т о г о  ключа dB абонента B
> # -----------------------------------------------------
> dB:=cB &^ (phi(p-1)-1) mod (p-1);
> # -------------------------------
> # Кодируемый текст (m<p): 
> # ----------------------------
> m:=12345672341234112389 mod p;  
> # --------------------------------
> # Процесс передачи и приёма текста
> # --------------------------------
> m1:=m  &^ cA mod p;
> m2:=m1 &^ cB mod p;  
> m3:=m2 &^ dA mod p;  
> m4:=m3 &^ dB mod p;  # Расшифрованный текст
> # ---------------------------------------------------------
> res:=m - m4;         # Проверка результатов передачи текста
> #
> # -------------------------------------------------------
> # На сцене появляется Ева, которая желает прочесть текст.
> # Предположим, что ей известны:
> #   (1) алгоритм Шамира, выбранный абонентами;
> #   (2) открытый ключ p;
> #   (3) зашифрованные сообщения m1, m2, m3.
> #
> # Ева хочет узнать m4:=m3 &^ dB mod p, а для этого она
> # пытается найти dB, решая задачу дискретного логариф-
> # мирования относительно cB вида:
> #
> #  m2:=m1 &^ cB mod p,
> #  
> # но при этом Ева не обращает внимания на то, что при-
> # ступает к решению очень трудоёмкой задачи! 
> # Поэтому действия Евы очевидны (попутно вычисляем время):
> st:=time(): cB_Eva:=mlog(m2,m1,p); vrem_1:=time()-st;
> #
> # Выполним проверку найденного (если, конечно, это
> # удалось!) решения (попутно вычисляем время):
> # :
> st:=time(): test:=(m1 &^ cB_Eva - m2) mod p; vrem_2:=time()-st;
> #
> # Вычисление закрытого ключа dB:
> dB_Eva:=cB_Eva &^ (phi(p-1)-1) mod (p-1);
> #
> # Наконец, получение "копии" исходного текста:
> m4_Eva:=m3 &^ dB_Eva mod p;
> #
> res_Eva:=m4_Eva - m;   # Проверка результата взлома
> #
> 
> 
                          234711212312123211912341
                                     78
                                    10133
                          115560469577142278124017
                                   123457
                           67316187867416660759553
                            12345672341234112389
                           93212149251652869232087
                           59916659362538357250079
                          212889530139395833553268
                            12345672341234112389
                                      0
                                   123457
                                  1580.922
                                      0
                                     0.
                           67316187867416660759553
                            12345672341234112389
                                      0
